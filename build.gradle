buildscript {
    repositories {
        mavenLocal()
        maven {
            url 'http://bdp.sany.com.cn/nexus/content/groups/public/'
        }
    }

    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

configure(allprojects) { project ->
    group = PROJ_GROUP
    version = PROJ_VERSION + '-' + PROJ_ARCHETYPE
    ext.bboss_version = BBOSS_PROJ_VERSION
    ext.mongodbversion = mongodbversion
    ext.jacksonversion = jacksonversion
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "maven"
    if (project.getProperty('skipTest')) {
        compileTestJava.enabled = false
        processTestResources.enabled = false
        testClasses.enabled = false
        test.enabled = false
    }

    eclipse {
        jdt {
            //if you want to alter the java versions (by default they are configured with gradle java plugin settings):
            sourceCompatibility = SOURCE_COMPILER_LEVEL
            targetCompatibility = TARGET_COMPILER_LEVEL
            javaRuntimeName = "../../org.eclipse.jdt.launching.JRE_CONTAINER"
        }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = SOURCE_COMPILER_LEVEL
        targetCompatibility = TARGET_COMPILER_LEVEL
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc) {
        sourceCompatibility = JAVADOC_SOURCE_LEVEL
        targetCompatibility = JAVADOC_COMPILER_LEVEL
        options.encoding = 'UTF-8'
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    repositories {
        mavenLocal()
        maven {
            url 'http://bdp.sany.com.cn/nexus/content/groups/public/'
        }
    }
}

configure(subprojects) { subproject ->
    task sourcesJar(type: Jar) {
        classifier = "sources"
        from sourceSets.main.allJava
    }

    task javaDocJar(type: Jar, dependsOn: javadoc) {
        classifier = "javadoc"
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javaDocJar
    }
}

configure([project(":bboss-security"), project(":bboss-security-web"), project(":bboss-security-web-inf")]) { subproject ->
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javaDocJar
            }
        }
        repositories {
            maven {
                credentials {
                    username 'sany'
                    password 'sany'
                }
                if(project.version.endsWith('-SNAPSHOT')) {
                    url 'http://bdp.sany.com.cn/nexus/content/repositories/snapshots/'
                } else {
                    url 'http://bdp.sany.com.cn/nexus/content/repositories/releases/'
                }
            }
        }
    }
}

configure([project(":bboss-session"), project(":bboss-sessionmonitor")]) { subproject ->
    apply plugin: 'war'
    if (enable_gretty.equals("true")) {
        apply plugin: 'org.akhikhl.gretty'
        gretty {
            httpPort = 80
            contextPath = subproject.name
            jvmArgs = ['-Xmx1024M', '-XX:PermSize=128M', '-XX:MaxPermSize=256M']
        }
    }
    webAppDirName = 'WebRoot'
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javaDocJar
            }
        }
        repositories {
            maven {
                credentials {
                    username 'sany'
                    password 'sany'
                }
                if(project.version.endsWith('-SNAPSHOT')) {
                    url 'http://bdp.sany.com.cn/nexus/content/repositories/snapshots/'
                } else {
                    url 'http://bdp.sany.com.cn/nexus/content/repositories/releases/'
                }
            }
        }
    }
}